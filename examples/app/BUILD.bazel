# Copyright 2017 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package(default_visibility = ["//visibility:public"])

load("@build_bazel_rules_typescript//:defs.bzl", "ts_library", "ts_devserver")
load("@io_bazel_rules_closure//closure:defs.bzl", "closure_js_library", "closure_js_binary")
load("//internal:build_defs.bzl", "closure_ts_binary")
load("//examples/app:internal_node_module.bzl", "internal_node_module")

ts_library(
    name = "ts_library",
    srcs = ["main.ts"],
    deps = ["//examples/app/hello-world"],
)

# Needed because the devserver only loads static files that appear under this
# package.
genrule(
    name = "angular_js",
    srcs = ["//:node_modules/angular/angular.js"],
    outs = ["angular.js"],
    cmd = "cp $< $@",
)

STATIC_FILES = [
    "index.html",
    ":angular.js",   
    "//examples/app/hello-world:static-files",
]

# Create a live-reload enable development server:
#
# npm run devserver
# ibazel run //examples/angular:devserver
ts_devserver(
    name = "devserver",
    serving_path = "/bundle.js",
    static_files = STATIC_FILES,
    deps = [
        # The devserver uses RequireJS for module loading so we need to instruct it 
        # that `require('angular')` should load AngularJS from the window.
        ":angularjs_shim.requirejs.js",
        ":ts_library"
    ],
    entry_module = "build_bazel_rules_typescript/examples/app/main",
)

# We do not want closure to compile angular.js, and instead will serve angular.min.js.
# Serving this file makes angular availible on the window as a global variable.
#
# @types/angular defines angularjs as a CommonJS module with an import path of 'angular'
# The shim exports the global variable to make it importable from the import path.
internal_node_module(
    name = "angularjs_shim_commonjs",
    import_path = "angular",
    main = "angularjs_shim.commonjs.js",
)

# Since we do not process AngularJS as a ts_library, we need to supply our own externs.
closure_js_library(
    name = "angularjs_externs",
    srcs = ["//:node_modules/google-closure-compiler/contrib/externs/angular-1.6.js"],
)

# Create a closure minified production bundle.
closure_ts_binary(
    name = "bundle",
    defs = [
        "--jscomp_off=checkTypes",
        "--jscomp_off=checkDebuggerStatement",
        # Needed to process the shim's commonJS module.
        "--process_common_js_modules",
    ],
    dependency_mode = "STRICT",
    entry_points = ["node_modules/build_bazel_rules_typescript/examples/app/main"],
    deps = [
        ":angularjs_externs",
        ":angularjs_shim_commonjs",
        ":ts_library",
    ],
)

# Create a production server to serve the bundle produced by closure_ts_binary.
#
# npm run prodserver
# ibazel run //examples/angular:devserver
ts_devserver(
    name = "prodserver",
    static_files = STATIC_FILES + [":bundle.js"],
)

ts_library(
    name = "e2e",
    testonly = 1,
    srcs = ["main_e2e_test.ts"],
)
